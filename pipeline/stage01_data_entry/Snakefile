# Stage 01 Data Curation

configfile: "config.yaml"
report: "report.rst"

import os
import sys
utils_path = '../'
sys.path.append(utils_path)
add_utils = f"export PYTHONPATH='$PYTHONPATH:{utils_path}'"

from settings import output_path
from utils import ordereddict_to_dict
output_path = os.path.join(output_path, 'stage01_data_entry/')


neo_format = '.nix' # fixed! Do not change!
STAGE_OUTPUT = config["OUTPUT"]
USE_LINK_AS_STAGE_OUTPUT = config["USE_LINK_AS_STAGE_OUTPUT"]

DATA_SETS = config["DATA_SETS"]
CURATION_SCRIPT = config["CURATION_SCRIPT"]
SPATIAL_SCALE = config['SPATIAL_SCALE']
SAMPLING_RATE = config["SAMPLING_RATE"]
T_START = config["T_START"]
T_STOP = config["T_STOP"]
ANNOTATIONS = ordereddict_to_dict(config["ANNOTATIONS"])
ARRAY_ANNOTATIONS = ordereddict_to_dict(config["ARRAY_ANNOTATIONS"])
KWARGS = ordereddict_to_dict(config["KWARGS"])

PLOT_TSTART = config["PLOT_TSTART"]
PLOT_TSTOP = config["PLOT_TSTOP"]
PLOT_CHANNEL = config["PLOT_CHANNEL"]
PLOT_FORMAT = config["PLOT_FORMAT"]

def input_file(wildcards):
    return DATA_SETS[wildcards.data_name]

rule all:
    input:
        data = expand(os.path.join(output_path, 'output_{name}'+neo_format),
               name = DATA_SETS.keys()),
        check = expand(os.path.join(output_path, 'output_{name}.check'),
               name = DATA_SETS.keys())
    params:
        command = 'ln -s' if USE_LINK_AS_STAGE_OUTPUT else 'cp'
    output:
        os.path.join(output_path, STAGE_OUTPUT)
    shell:
        """
        {params.command} "{input.data[0]}" "{output}"
        """

rule check_input:
    # ToDo!
    input:
        data = os.path.join(output_path, 'output_{data_name}'+neo_format)
    output:
        temp(os.path.join(output_path, 'output_{data_name}.check'))
    shell:
        """
        touch "{output}"
        """

rule enter_data:
    input:
        data = input_file,
        script = os.path.join('scripts/', CURATION_SCRIPT),
        plot_script = 'scripts/plot_traces.py'
    output:
        data = os.path.join(output_path, 'output_{data_name}'+neo_format),
        img = report(os.path.join(output_path, 'trace_{data_name}'+PLOT_FORMAT))
    params:
        spatial_scale = SPATIAL_SCALE,
        sampling_rate = SAMPLING_RATE,
        t_start = T_START,
        t_stop = T_STOP,
        annotations = ANNOTATIONS,
        array_annotations = ARRAY_ANNOTATIONS,
        kwargs = KWARGS,
        plot_tstart = PLOT_TSTART,
        plot_tstop = PLOT_TSTOP,
        plot_channel = PLOT_CHANNEL
    shell:
        """
        {add_utils}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --sampling_rate {params.sampling_rate} \
                              --spatial_scale {params.spatial_scale} \
                              --t_start {params.t_start} \
                              --t_stop {params.t_stop} \
                              --data_name {wildcards.data_name} \
                              --annotations {params.annotations} \
                              --array_annotations {params.array_annotations} \
                              --kwargs {params.kwargs}
        python {input.plot_script} --data "{output.data}" \
                                   --output "{output.img}" \
                                   --t_start {params.plot_tstart} \
                                   --t_stop {params.plot_tstop} \
                                   --channel {params.plot_channel} \
        """
