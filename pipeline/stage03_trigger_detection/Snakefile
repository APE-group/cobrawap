# Stage 03 Trigger Detection

configfile: "config.yaml"
report: "report.rst"

import os
import sys
utils_path = '../'
add_utils = f"export PYTHONPATH='$PYTHONPATH:{utils_path}'"

sys.path.append('../')
from settings import output_path
from utils import parse_plot_channels

if 'INPUT_FILE' in config:
    stage_input = config["INPUT_FILE"]
else:
    stage_input = os.path.join(output_path, 'stage02_processing',
                               'processed_data.nix')

output_path = os.path.join(output_path, 'stage03_trigger_detection/')

neo_format = '.nix' # fixed! Do not change!
STAGE_OUTPUT = config["OUTPUT"]
USE_LINK_AS_STAGE_OUTPUT = config["USE_LINK_AS_STAGE_OUTPUT"]
PLOT_FORMAT = config["PLOT_FORMAT"]

DETECTION_BLOCK = config["DETECTION_BLOCK"]
PLOT_TSTART = config["PLOT_TSTART"]
PLOT_TSTOP = config["PLOT_TSTOP"]
PLOT_CHANNEL = parse_plot_channels(config["PLOT_CHANNEL"], stage_input)

THRESHOLD_METHOD = config["THRESHOLD_METHOD"]
FIXED_THRESHOLD = config["FIXED_THRESHOLD"]
FIT_FUNCTION = config["FIT_FUNCTION"]
BIN_NUM = config["BIN_NUM"]
SIGMA_FACTOR = config["SIGMA_FACTOR"]

TRANSITION_PHASE = config["TRANSITION_PHASE"]
MINIMA_ORDER = config["MINIMA_ORDER"]

FRAME_RATE = config["FRAME_RATE"]
QUALITY = config["QUALITY"]
SCALE_X = config["SCALE_X"]
SCALE_Y = config["SCALE_Y"]
FPS = config["FPS"]
BITRATE = config["BITRATE"]
COLORMAP = config["COLORMAP"]


rule all:
    input:
        data = os.path.join(output_path, DETECTION_BLOCK, 'transitions'+neo_format),
        plot_script = 'scripts/plot_trigger_times.py',
    params:
        tstart = PLOT_TSTART,
        tstop = PLOT_TSTOP,
        channel = PLOT_CHANNEL,
        img = ','.join([os.path.join(output_path, DETECTION_BLOCK,
                        f'transitions_channel{channel}'+PLOT_FORMAT)
                        for channel in PLOT_CHANNEL]),
        command = 'ln -s' if USE_LINK_AS_STAGE_OUTPUT else 'cp'
    output:
        data = os.path.join(output_path, STAGE_OUTPUT),
        img = report(os.path.join(output_path, DETECTION_BLOCK,
                     f'transitions_channel{PLOT_CHANNEL[0]}'+PLOT_FORMAT))
    shell:
        """
        {add_utils}
        {params.command} "{input.data}" "{output.data}"
        python {input.plot_script} --data "{input.data}" \
                                   --output "{params.img}" \
                                   --t_start {params.tstart} \
                                   --t_stop {params.tstop} \
                                   --channel {params.channel} \
        """

rule threshold:
    input:
        data = stage_input,
        thresholds = os.path.join(output_path, 'threshold',
                                  '{}_thresholds.npy'.format(THRESHOLD_METHOD)),
        script = 'scripts/threshold.py'
    output:
        data = os.path.join(output_path, 'threshold', 'transitions'+neo_format)
    shell:
        """
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --thresholds "{input.thresholds}"
        """

rule calc_tresholds_fixed:
    input:
        data = stage_input,
        script = 'scripts/calc_thresholds_fixed.py'
    params:
        threshold = FIXED_THRESHOLD
    output:
        array = os.path.join(output_path, 'threshold', 'fixed_thresholds.npy')
    shell:
        """
        python {input.script} --data "{input.data}" \
                              --output "{output.array}" \
                              --threshold {params.threshold}
        """

rule calc_tresholds_fitted:
    input:
        data = stage_input,
        script = 'scripts/calc_thresholds_fitted.py'
    params:
        sigma_factor = SIGMA_FACTOR,
        fit_function = FIT_FUNCTION,
        bin_num = BIN_NUM,
        plot_channel = PLOT_CHANNEL
    output:
        array = os.path.join(output_path, 'threshold', 'fitted_thresholds.npy'),
        img = report(os.path.join(output_path, 'threshold',
                     'amplitude_distribution_channel{}{}'\
                     .format(PLOT_CHANNEL, PLOT_FORMAT)))
    shell:
        """
        {add_utils}
        python {input.script} --data "{input.data}" \
                              --output "{output.array}" \
                              --output_img "{output.img}" \
                              --sigma_factor {params.sigma_factor} \
                              --fit_function {params.fit_function} \
                              --bin_num {params.bin_num} \
                              --plot_channel {params.plot_channel}
        """

rule hilbert_phase:
    input:
        data = stage_input,
        script = 'scripts/hilbert_phase.py'
    params:
        transition_phase = TRANSITION_PHASE
    output:
        data = os.path.join(output_path, 'hilbert_phase', 'transitions'+neo_format)
    shell:
        """
        {add_utils}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --transition_phase {params.transition_phase}
        """

rule minima:
    input:
        data = stage_input,
        script = 'scripts/minima.py'
    params:
        order = MINIMA_ORDER
    output:
        data = os.path.join(output_path, 'minima', 'transitions'+neo_format)
    shell:
        """
        {add_utils}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --order {params.order}
        """

### Movie plotting rules

rule time_slice:
    input:
        data = '{file_path}.nix',
        script = 'scripts/time_slice.py'
    output:
        data = '{file_path}_{t_start}-{t_stop}s.nix'
    shell:
        """
        {add_utils}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --t_start {wildcards.t_start} \
                              --t_stop {wildcards.t_stop} \
        """

rule plot_movie_frames:
    input:
        data = os.path.join('{path}', '{file}.nix'),
        script = "scripts/plot_movie_frames.py",
    params:
        frame_name = 'frame',
        frame_format = PLOT_FORMAT,
        colormap = COLORMAP,
        frame_rate = FRAME_RATE
    output:
        frame_folder = directory(os.path.join('{path}', '{file}_frames')),
    shell:
        """
        {add_utils}
        python {input.script} --data "{input.data}" \
                              --frame_folder "{output.frame_folder}" \
                              --frame_format {params.frame_format} \
                              --frame_name {params.frame_name} \
                              --frame_rate {params.frame_rate} \
                              --colormap {params.colormap}
        """

rule plot_movie:
    input:
        os.path.join('{path}', '{file}_frames')
    output:
        os.path.join('{path}', '{file}.mp4')
    params:
        frame_path = lambda wildcards, input: os.path.join(input[0],
                                                'frame_%05d{}'\
                                                .format(PLOT_FORMAT)),
        quality = QUALITY,
        scale_x = SCALE_X,
        scale_y = SCALE_Y,
        bitrate = BITRATE,
        fps = FPS
    shell:
        """
        ffmpeg -y -framerate {params.fps} \
               -i "{params.frame_path}" \
               -crf {params.quality} \
               -vb {params.bitrate} \
               -vcodec libx264 \
               -vf scale={params.scale_x}:{params.scale_y} \
               "{output}"
        """

rule mp4_to_webm:
    input:
        '{path}.mp4'
    output:
        '{path}.webm'
    shell:
        """
        ffmpeg -i "{input}" -vcodec libvpx -acodec libvorbis "{output}"
        """

rule mp4_to_gif:
    input:
        '{path}.mp4'
    output:
        '{path}_{scale}px.gif'
    shell:
        """
        ffmpeg -i "{input}" -vf "scale={wildcards.scale}:-1:flags=lanczos,split[s0][s1];[s0]palettegen[p];[s1][p]paletteuse" -loop 0 {output}
        """
