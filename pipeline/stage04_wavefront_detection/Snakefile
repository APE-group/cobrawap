# Stage 04 Wavefront Detection

configfile: "config.yaml"
report: "report.rst"

import os
import sys
utils_path = '../'
sys.path.append(utils_path)

from settings import output_path
from utils import parse_plot_channels

STAGE_NAME = config["STAGE_NAME"]
STAGE_OUTPUT = config["STAGE_OUTPUT"]
NEO_FORMAT = config["NEO_FORMAT"]
USE_LINK_AS_STAGE_OUTPUT = config["USE_LINK_AS_STAGE_OUTPUT"]
ADD_UTILS = f"export PYTHONPATH='$PYTHONPATH:{utils_path}'"
OUTPUT_DIR = os.path.join(output_path, STAGE_NAME)

if 'STAGE_INPUT' in config and config['STAGE_INPUT'] is not None:
    STAGE_INPUT = config["STAGE_INPUT"]
else:
    STAGE_INPUT = os.path.join(output_path, 'stage03_trigger_detection',
                               'trigger_times.nix')

PLOT_TSTART = config["PLOT_TSTART"]
PLOT_TSTOP = config["PLOT_TSTOP"]
PLOT_CHANNELS = parse_plot_channels(config["PLOT_CHANNELS"], STAGE_INPUT)
PLOT_FORMAT = config["PLOT_FORMAT"]

TRIGGER_FILTER = config["TRIGGER_FILTER"]
WAVEFRONT_FILTER = config["WAVEFRONT_FILTER"]
METRIC = config["METRIC"]
TIME_DIM = config["TIME_DIM"]
NEIGHBOUR_DISTANCE = config["NEIGHBOUR_DISTANCE"]
MIN_SAMPLES_PER_WAVE = config["MIN_SAMPLES_PER_WAVE"]

def clustering_input(wildcards):
    if len(TRIGGER_FILTER):
        return os.path.join(OUTPUT_DIR, TRIGGER_FILTER[-1], 'trigger_times.nix')
    else:
        return STAGE_INPUT

rule all:
    input:
        data = os.path.join(OUTPUT_DIR, 'clustering', STAGE_OUTPUT),
        plot_script = 'scripts/plot_clustering.py'
    params:
        command = 'ln -s' if USE_LINK_AS_STAGE_OUTPUT else 'cp'
    output:
        data = os.path.join(OUTPUT_DIR, STAGE_OUTPUT),
        img = report(os.path.join(OUTPUT_DIR, f'cluster.{PLOT_FORMAT}'))
    shell:
        """
        {ADD_UTILS}
        {params.command} "{input.data}" "{output.data}"
        python {input.plot_script} --data "{output.data}" \
                                   --output "{output.img}" \
        """

#### Blocks
# ToDo: rule remove_short_states

rule clustering:
    input:
        data = clustering_input,
        script = 'scripts/clustering.py',
        plot_script = 'scripts/plot_clustering.py'
    params:
        metric = METRIC,
        time_dim = TIME_DIM,
        neighbour_distance = NEIGHBOUR_DISTANCE,
        min_samples = MIN_SAMPLES_PER_WAVE
    output:
        data = os.path.join(OUTPUT_DIR, 'clustering', STAGE_OUTPUT),
        img = report(os.path.join(OUTPUT_DIR, 'clustering', 'wave_clustering.'+PLOT_FORMAT))
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --metric {params.metric} \
                              --time_dim {params.time_dim} \
                              --neighbour_distance {params.neighbour_distance} \
                              --min_samples {params.min_samples}
        python {input.plot_script} --data "{output.data}" \
                                   --output "{output.img}"
        """
